Private Sub GerarGraficoDeLinhas(setor As String, wsTemp As Worksheet, ultimalinha As Long, _
    linCabecalho As Long, colunas As Variant, posTop As Double, titulo As String, _
    formatacao As String, porcentagem As Boolean)

    Dim ch As ChartObject
    Dim i As Variant
    Dim rngX As Range
    Dim rngY As Range
    Dim minY As Double, maxY As Double

    ' Cria o gráfico
    Set ch = wsTemp.ChartObjects.Add(Left:=10, Top:=posTop, Width:=396, Height:=178)

    With ch.Chart
        .ChartType = xlLine

        ' Define o eixo X (Ano + Mês)
        Set rngX = wsTemp.Range("A" & linCabecalho + 1 & ":B" & ultimalinha)

        ' Reset min/max
        minY = 1E+99
        maxY = -1E+99

        ' Adiciona cada série
        For i = LBound(colunas) To UBound(colunas)
            Set rngY = wsTemp.Range(wsTemp.Cells(linCabecalho + 1, colunas(i)), wsTemp.Cells(ultimalinha, colunas(i)))
            
            .SeriesCollection.NewSeries
            .SeriesCollection(.SeriesCollection.Count).Name = wsTemp.Cells(linCabecalho, colunas(i)).Value
            .SeriesCollection(.SeriesCollection.Count).XValues = rngX
            .SeriesCollection(.SeriesCollection.Count).Values = rngY

            ' Atualiza min/max do eixo Y
            If Application.Min(rngY) < minY Then minY = Application.Min(rngY)
            If Application.Max(rngY) > maxY Then maxY = Application.Max(rngY)
        Next i

        ' Título
        .HasTitle = True
        With .ChartTitle
            .Text = titulo & " | " & setor
            .Font.Size = 14
            .Font.Bold = False
            .Font.Color = RGB(89, 89, 89)
        End With

        ' Eixo Y (dinâmico com MajorUnit controlado)
        With .Axes(xlValue)
            If porcentagem = True Then
                .MinimumScale = 0
                .MaximumScale = 0.8
                .MajorUnit = 0.2
            Else
                .MinimumScale = WorksheetFunction.RoundDown(minY, 0)
                .MaximumScale = WorksheetFunction.RoundUp(maxY, 0)
                
                faixa = .MaximumScale - .MinimumScale
                
                Select Case True
                    Case faixa < 1
                        .MajorUnit = 0.5
                    Case faixa < 6
                        .MajorUnit = 1
                    Case Else
                        .MajorUnit = 2
                End Select
            End If
            
            ' Formatação do eixo Y (sempre aplicada)
            .TickLabels.NumberFormat = formatacao
            .TickLabels.Font.Size = 9
            .TickLabels.Font.Color = RGB(89, 89, 89)
            .Format.Line.Visible = msoFalse
            .HasMajorGridlines = True
        End With

        ' Eixo X
        With .Axes(xlCategory).TickLabels.Font
            .Size = 9
            .Color = RGB(89, 89, 89)
        End With

        ' Legenda ? só mostra se houver mais de 1 série
        .HasLegend = (UBound(colunas) > LBound(colunas))
        If .HasLegend Then .Legend.Position = xlLegendPositionBottom

        .Axes(xlCategory).HasTitle = False
        .Axes(xlValue).HasTitle = False
    End With
    Debug.Print ("Gerando gráfico " & titulo)
    
End Sub

Private Sub PreencheIntervaloApartirTabDinamica(setor As String, wsTemp As Worksheet, sheet As String)
    Dim wsPivot As Worksheet
    Dim pt As PivotTable
    Dim rngTemp As Range
    Dim i As Long
    
    ' Get/pega tabela dinâmica
    Set wsPivot = ThisWorkbook.Sheets(sheet)
    On Error Resume Next
    Set pt = wsPivot.PivotTables(1)
    If pt Is Nothing Then
        MsgBox "Tabela Dinâmica não encontrada!"
        Exit Sub
    End If
    On Error GoTo 0

    ' Filtra a Tabela Dinâmica
    Call FiltrarSlicerPorSetor(setor)
    On Error Resume Next
    If wsTemp Is Nothing Then
        Set wsTemp = ThisWorkbook.Sheets.Add
        wsTemp.Name = SHEET_TEMPORARIO
    Else
        wsTemp.Cells.Clear
    End If
    On Error GoTo 0

    ' Copia dados filtrados
    pt.TableRange2.Copy
    wsTemp.Range("A1").PasteSpecial Paste:=xlPasteValues
    Application.CutCopyMode = False
    wsTemp.Range("A2", "E100").NumberFormat = "0.00"
 
End Sub

Private Sub MesclarColunaAno(ws As Worksheet, ByVal colAno As Long, ByVal primeiraLinha As Long, ByVal ultimalinha As Long)
    Dim i As Long, j As Long
    Dim v As String, prox As String

    'garante que não há mesclas antigas
    ws.Range(ws.Cells(primeiraLinha, colAno), ws.Cells(ultimalinha, colAno)).UnMerge

    i = primeiraLinha
    Do While i <= ultimalinha
        j = i
        Do While j < ultimalinha
            v = Trim$(CStr(ws.Cells(j, colAno).Value))
            prox = Trim$(CStr(ws.Cells(j + 1, colAno).Value))
            If v <> prox Then Exit Do
            j = j + 1
        Loop

        If j > i Then
            With ws.Range(ws.Cells(i, colAno), ws.Cells(j, colAno))
                .Merge
                .HorizontalAlignment = xlCenter
                .VerticalAlignment = xlCenter
            End With
        End If

        i = j + 1
    Loop
End Sub

Public Sub GerarGraficosTeste(setor As String)
    Dim wsAvaTemp As Worksheet, wsFelTemp As Worksheet
    Dim rngTemp As Range
    Dim ultimalinha As Long, ultimaCol As Long
    Dim coluna As Long
    Dim posTop As Double
    
    Set wsAvaTemp = ThisWorkbook.Sheets("AvaTemp")
    Set wsFelTemp = ThisWorkbook.Sheets("FelTemp")
    
    'SGP
    Call PreencheIntervaloApartirTabDinamica(setor, wsAvaTemp, "DinSGP")
    
    Set rngTemp = wsAvaTemp.Range("A1").CurrentRegion
    ultimalinha = wsAvaTemp.Cells(wsAvaTemp.Rows.Count, "A").End(xlUp).Row
    ultimaCol = wsAvaTemp.Cells(1, wsAvaTemp.Columns.Count).End(xlToLeft).Column
    
    Application.DisplayAlerts = False
    Call MesclarColunaAno(wsAvaTemp, 1, 2, ultimalinha)
    Application.DisplayAlerts = True
    
    Call GerarGraficoDeLinhas(setor, wsAvaTemp, ultimalinha, 2, Array(3, 4, 5), 200, "Evolução das categorias das avaliações da SGP", "#.##0,0%", True)
    
    'CBEN
    Call PreencheIntervaloApartirTabDinamica(setor, wsAvaTemp, "DinCBEN")
    
    Set rngTemp = wsAvaTemp.Range("A1").CurrentRegion
    ultimalinha = wsAvaTemp.Cells(wsAvaTemp.Rows.Count, "A").End(xlUp).Row
    ultimaCol = wsAvaTemp.Cells(1, wsAvaTemp.Columns.Count).End(xlToLeft).Column
    
    Application.DisplayAlerts = False
    Call MesclarColunaAno(wsAvaTemp, 1, 2, ultimalinha)
    Application.DisplayAlerts = True
    
    Call GerarGraficoDeLinhas(setor, wsAvaTemp, ultimalinha, 2, Array(3, 4, 5), 400, "Evolução das categorias das avaliações da CBEN", "0,0%", True)
    
    'Chefia imediata
    Call PreencheIntervaloApartirTabDinamica(setor, wsAvaTemp, "DinCI")
    
    Set rngTemp = wsAvaTemp.Range("A1").CurrentRegion
    ultimalinha = wsAvaTemp.Cells(wsAvaTemp.Rows.Count, "A").End(xlUp).Row
    ultimaCol = wsAvaTemp.Cells(1, wsAvaTemp.Columns.Count).End(xlToLeft).Column
    
    Application.DisplayAlerts = False
    Call MesclarColunaAno(wsAvaTemp, 1, 2, ultimalinha)
    Application.DisplayAlerts = True

    Call GerarGraficoDeLinhas(setor, wsAvaTemp, ultimalinha, 2, Array(3, 4, 5), 700, "Evolução das categorias das avaliações da chefia imediata", "0,0%", True)
End Sub

